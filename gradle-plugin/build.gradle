plugins {
    id 'java'
}

group = 'net.example'
version = '1.0.0'

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes(
                'Main-Class': 'net.example.ExampleMod.java'
        )
    }
}

tasks.register("publishToModtale", Exec) {
    group = "publishing"
    description = "Publishes the built jar to Modtale.net."

    dependsOn tasks.named("build")

    def curlExe = org.gradle.internal.os.OperatingSystem.current().isWindows()
            ? "C:/Windows/System32/curl.exe"
            : "curl"

    doFirst {
        def apiKey = System.getenv("MODTALE_KEY")
        def projectId = System.getenv("MODTALE_PROJECT_ID")

        def jarFile = layout.buildDirectory
                .file("libs/${project.name}-${project.version}.jar")
                .get().asFile

        if (!apiKey || !projectId) {
            throw new GradleException("Error: MODTALE_KEY and MODTALE_PROJECT_ID env vars are required.")
        }
        if (!jarFile.exists()) {
            throw new GradleException("Error: Jar file not found: ${jarFile}")
        }

        commandLine(
                curlExe, '-X', 'POST',
                "https://api.modtale.net/api/v1/projects/${projectId}/versions",
                '-H', "X-MODTALE-KEY: ${apiKey}",
                '-F', "file=@${jarFile.absolutePath}",
                '-F', "versionNumber=${project.version}",
                // RELEASE | BETA | ALPHA   
                '-F', "channel=RELEASE",
                // Comma-separated list (Release 1.0, Release 1.1)
                '-F', "gameVersions=Release 1.0",
                // Markdown string
                '-F', "changelog=Uploaded via Local Gradle Task"
        )
    }
}


