plugins {
    id 'java'
}

group = 'net.example'
version = '1.0.0'

repositories {
    mavenCentral()
}

// 1. Create the Jar
jar {
    manifest {
        attributes(
                'Main-Class': 'net.example.ExampleMod.java'
        )
    }
}

// 2. Publish Task
tasks.register("publishToModtale", Exec) {
    group = "publishing"
    description = "Publishes the built jar to Modtale.net."

    dependsOn tasks.named("build")

    def curlExe = org.gradle.internal.os.OperatingSystem.current().isWindows()
            ? "C:/Windows/System32/curl.exe"
            : "curl"

    doFirst {
        def apiKey = System.getenv("MODTALE_KEY")
        def projectId = System.getenv("MODTALE_PROJECT_ID")

        def jarFile = layout.buildDirectory
                .file("libs/${project.name}-${project.version}.jar")
                .get().asFile

        if (!apiKey || !projectId) {
            throw new GradleException("Error: MODTALE_KEY and MODTALE_PROJECT_ID env vars are required.")
        }
        if (!jarFile.exists()) {
            throw new GradleException("Error: Jar file not found: ${jarFile}")
        }

        commandLine(
                curlExe, '-X', 'POST',
                "https://modtale.net/api/v1/publish/${projectId}",
                '-H', "X-Api-Key: ${apiKey}",
                '-F', "file=@${jarFile.absolutePath}",
                '-F', "versionNumber=${project.version}",
                '-F', "gameVersions=Release 1.0",
                '-F', "changelog=Uploaded via Local Gradle Task"
        )
    }
}

